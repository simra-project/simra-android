plugins {
    id 'kotlin-android'
    id 'com.android.application'
}

def ENV_VARIABLE_PREFIX = "SIMRA_"
def API_ENDPOINT = "API_ENDPOINT"
def API_VERSION = "API_VERSION"

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "de.tuberlin.mcc.simra.app"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 94
        versionName "94.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // defining the build date
        buildConfigField "long", "BUILD_DATE", System.currentTimeMillis() + "L"

        // Load `local.properties` (from root) for development configuration
        Properties properties = new Properties()
        if (project.rootProject.file('local.properties').exists()) {
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
        }


        buildConfigField "String", API_ENDPOINT, properties.getProperty(API_ENDPOINT) ?: "\"https://vm2.mcc.tu-berlin.de:8082/\""
        buildConfigField "String", API_VERSION, properties.getProperty(API_VERSION) ?: "\"13/\""

    }
    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Allow loading the configuration Settings from environment variables
            def VAR_API_ENDPOINT = System.getenv(ENV_VARIABLE_PREFIX + API_ENDPOINT)
            if (VAR_API_ENDPOINT != null) {
                buildConfigField "String", API_ENDPOINT, "\"" + VAR_API_ENDPOINT + "\""
            }
            def VAR_API_VERSION = System.getenv(ENV_VARIABLE_PREFIX + API_VERSION)
            if (VAR_API_VERSION != null) {
                buildConfigField "String", API_VERSION, "\"" + VAR_API_VERSION + "\""
            }
        }
        qa {
            initWith buildTypes.release
        }
    }
    buildFeatures {
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility = '1.9'
        targetCompatibility = '1.9'
        sourceCompatibility JavaVersion.VERSION_1_9
        targetCompatibility JavaVersion.VERSION_1_9
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.lifecycle:lifecycle-service:2.3.1"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    api 'org.osmdroid:osmdroid-android:6.1.10'
    api 'com.github.MKergall:osmbonuspack:6.7.0'
    implementation 'com.github.Jay-Goo:RangeSeekBar:v2.0.4'
    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.4'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.17'

    // RingBuffer
    implementation 'org.apache.commons:commons-collections4:4.4'

    // Tests
    testImplementation 'junit:junit:4.13'
    testImplementation "org.assertj:assertj-core:3.16.1"

    // "Backporting" Java 8 streams for Android < 24
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.2'

    // Kotlin
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.0"

}
